Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
IF
CharacterUsedSkillAtPosition(_Character, _X, _Y, _Z, "Projectile_LaunchExplosiveTrap", _, _)
THEN
DB_OBPYRO_Trap_Spawn(_Character, _X, _Y, _Z);
ProcObjectTimer(_Character, "OdinPYRO_TRAP_SPAWN", 2000);
ApplyStatus(_Character, "OdinNECRO_DEBUG_AP_ADD", 0.0);

IF
CharacterUsedSkillAtPosition(_Character, _X, _Y, _Z, "Projectile_DeployMassTraps", _, _)
THEN
DB_OBPYRO_Trap_Spawn(_Character, _X, _Y, _Z);
ProcObjectTimer(_Character, "OdinPYRO_TRAP_SPAWN", 2000);
ApplyStatus(_Character, "OdinNECRO_DEBUG_AP_ADD", 0.0);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Character, "OdinPYRO_TRAP_SPAWN")
AND
DB_OBPYRO_Trap_Spawn(_Character, _X, _Y, _Z)
AND
CreateItemTemplateAtPosition("b563cfc6-e0e2-4371-8f95-333dfeb168e5", _X, _Y, _Z, _Trap)
AND
CharacterGetLevel(_Character, _Level)
THEN
ApplyStatus(_Character, "OdinNECRO_DEBUG_AP_REMOVE", 0.0);
NOT DB_OBPYRO_Trap_Spawn(_Character, _X, _Y, _Z);
ItemLevelUpTo(_Trap, _Level);
ItemSetOwner(_Trap, _Character);
DB_OBPYRO_ExplosiveTrap(_Trap, _Character);

IF
CharacterStatusApplied(_Character, "OdinPYRO_PROC_TRAPDAMAGE", _Trap)
AND
DB_OBPYRO_ExplosiveTrap((ITEMGUID)_Trap, _Owner)
THEN
OdinPYRO_ApplyTrapDamage(_Character, _Owner, _Trap);

IF
CharacterStatusApplied(_Character, "OdinPYRO_PROC_TRAPDAMAGE", (CHARACTERGUID)_Owner)
AND
DB_OBPYRO_ExplosiveTrap(_Trap, _Owner)
THEN
OdinPYRO_ApplyTrapDamage(_Character, _Owner, _Trap);

PROC
OdinPYRO_ApplyTrapDamage((CHARACTERGUID)_Character, (CHARACTERGUID)_Owner, (ITEMGUID)_Trap)
AND
HasActiveStatus(_Character, "OdinPYRO_TRAPDAMAGE", 0)
THEN
ProcObjectTimerCancel(_Trap, "OdinPYRO_TRAP_CLEANUP");
ProcObjectTimer(_Trap, "OdinPYRO_TRAP_CLEANUP", 125);
ApplyStatus(_Character, "OdinPYRO_TRAPDAMAGE", 0.0, 1, _Owner);
ApplyStatus(_Character, "BURNING", 6.0, 0, _Owner);
ApplyStatus(_Character, "OdinPyro_SNARED", 6.0, 0, _Owner);
ApplyStatus(_Character, "OdinPyro_PROC_SCORCH", 0.0, 1, _Owner);

PROC
ProcObjectTimerFinished((ITEMGUID)_Trap, "OdinPYRO_TRAP_CLEANUP")
AND
DB_OBPYRO_ExplosiveTrap(_Trap, _Owner)
THEN
NOT DB_OBPYRO_ExplosiveTrap(_Trap, _Owner);
EXITSECTION

ENDEXITSECTION
